10 REM main program
20 DEV=100:REM SD card floppy device number
30 CALL CATALOG(DEV,F$)
40 REM
90 REM ### split files string ####
110 DIM P$(100)
120 C=1:NP=1
130 REM
140 D=POS(F$,",",C+1)
145 IF D=0 THEN D=LEN(F$)+1
150 P$(NP)=SEG$(F$,C,D-C)
160 IF D>LEN(F$)THEN GOTO 200
170 C=D+1
175 NP=NP+1
176 IF NP>100 THEN GOTO 200
180 GOTO 140
200 REM ### select program ###
210 I=1
220 CALL KEY(K,S)
230 IF K=232 THEN I=I+1:GOTO 250
240 IF K=233 THEN I=I-1:GOTO 250
241 IF K=13 THEN GOTO 400
250 IF I>NP THEN I=1
260 IF I<1 THEN I=NP
270 PRINT P$(I),I
280 GOTO 220
400 CALL RUNPROG(DEV,P$(I))
410 GOTO 220
490 REM ### run selected program ###
495 SUB RUNPROG(DEV,PROG$)
500 PRINT "RUN """&PROG$&""" ? [YN]"
501 A$=KEY$
502 IF A$<>"Y"AND A$<>"y"THEN GOTO 510
505 RUN STR$(DEV)&"."&PROG$
510 SUBEND
1000 REM ### read the catalog file ###
1010 SUB CATALOG(DEV,F$)
1020 CALL IO(DEV,14)
1025 PAUSE 1
1030 CATFILE$=STR$(DEV)&".CATALOG"
1040 OPEN #20,CATFILE$,DISPLAY,INPUT
1050 INPUT #20,F$
1060 CLOSE #20
1070 SUBEND
