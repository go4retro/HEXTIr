  100 ! Demo for use of internal/relative files with TIIF/HEXTIr
  110 IMAGE #### ###### ###.###### ###%
  120 ! 20 values, a record contains 3 dates of maximum 9 Byte each
  130 RANDOMIZE:N=20:BUF=3*9:A$="100.heapsort.int"
  140 ! set the requiered attributes. The extension .int is added automatically
  150 OPEN #1,A$,OUTPUT,INTERNAL,VARIABLE BUF,RELATIVE
  160 ! save the number of datasets and recordsize in the file as the first record
  170 PRINT #1,REC 0,BUF,N
  180 FOR I=1 TO N
  190 ! now generate the data-record ...
  200 X=INT(RND*1000):Y=X^2:Z=SQR(X)
  210 ! and write it to the file
  220 PRINT #1,X,Y,Z
  230 PRINT USING 110,X,Y,Z,INT(I/N*100)
  240 NEXT
  250 ! the file is created
  260 CLOSE #1
  270 ! setup new attributes in order to get the real used attributes
  280 ! test#2 means, that we will read two datasets in one record
  290 ! this speeds up datatransfer, since we will read only one record
  300 OPEN #1,A$,INPUT,INTERNAL,VARIABLE 2*9
  310 ! Get the parameters!
  320 INPUT #1,BUF,N
  330 ! ... and close the file
  340 CLOSE #1
  350 ! and reopen it again with the new attributes
  360 OPEN #1,A$,UPDATE,INTERNAL,VARIABLE BUF,RELATIVE
  370 ! Again we will not clear the screen (.c=n)
  380 K=0:GOTO 570
  390 ! Subroutine SiftDown. A SUB SiftDown would be possible too,
  400 ! but is not necessary. Therefore a simple GOSUB will do the job.
  410 I=L:J=2*I
  420 INPUT #1,REC I,X,Y,Z
  430 IF J>R THEN 540
  440 INPUT #1,REC J,HX
  450 IF J=R THEN 490
  460 INPUT #1,KX
  470 ! The increment of the record-pointer was done by the TI74 automatically
  480 IF KX>HX THEN J=J+1:HX=KX
  490 PRINT "*";:K=K+1:IF K=31 THEN K=0:PRINT
  500 IF X>=HX THEN 540
  510 INPUT #1,REC J,HX,HY,HZ
  520 PRINT #1,REC I,HX,HY,HZ
  530 I=J:J=2*I:GOTO 430
  540 PRINT #1,REC I,X,Y,Z
  550 RETURN! ends here
  560 ! Do a Heapsort on the file in O(N*ld(N))-time...
  570 PRINT "BuildHeap: ";
  580 L=INT(N/2)+1:R=N
  590 IF L=1 THEN 600 ELSE L=L-1:GOSUB 410:GOTO 590
  600 PRINT:PRINT "Sorting: ";
  610 IF R<=1 THEN 690
  620 INPUT #1,REC L,X,Y,Z
  630 INPUT #1,REC R,HX,HY,HZ
  640 PRINT #1,REC R,X,Y,Z
  650 PRINT #1,REC L,HX,HY,HZ
  660 R=R-1
  670 GOSUB 410!Call SiftDown
  680 GOTO 610
  690 PRINT
  700 CLOSE #1
  710 ! The file is sorted. OK to show the results...
  720 ! setup new attributes in order to get the parameters
  730 OPEN #1,A$,INPUT,INTERNAL
  740 ! Get the parameter! We don't need N here, only the recordsize!
  750 INPUT #1,BUF
  760 CLOSE #1
  770 ! Open the file with new attributes
  780 OPEN #1,A$,INPUT,INTERNAL,VARIABLE BUF,RELATIVE
  790 ! The first record (#0) contains the parameters, We are not interested in!
  800 RESTORE #1,REC 1
  810 ! Since we are reading sequential, we do need to know N and can use EOF
  820 IF EOF(1) THEN 860! Replacement for WHILE NOT EOF...
  830 INPUT #1,X,Y,Z
  840 PRINT USING 110,X,Y,Z:PAUSE .5
  850 GOTO 820
  860 CLOSE #1
  870 PAUSE 1:GOTO 150! repeat forever